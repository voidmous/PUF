#NoEnv  ; Recommended for performance and compatibility with future AutoHotkey releases.
; #Warn  ; Enable warnings to assist with detecting common errors.
#SingleInstance force
SendMode Input  ; Recommended for new scripts due to its superior speed and reliability.
SetWorkingDir %A_ScriptDir%  ; Ensures a consistent starting directory.

VERSION := 0.2
AUTHOR := joshz

DEBUGFLAG := False	; Output debug info if DEBUGFLAG is True
IniPath :=  A_ScriptDir  . "\PUF.ini"
SetTitleMatchMode, 2 	; Matching window whose title contain(not exactly equals to) WinTitle

;;;;;;;;;;;;;;;;;;;;;;;; Tray Menu Settings
Menu, TRAY, Tip, Pop Up Framework 	; Tray icon help info when hovering up
Menu, TRAY, Icon, %A_ScriptDir%\res\PUF.ico, 1, 1
Menu, TRAY, NoStandard
Menu, TRAY, add, Null
Menu, TRAY, Disable, Null
Menu, TRAY, Rename, Null, Ver %VERSION% ; Display Version
Menu, CFG,  add, OpenDir
Menu, CFG,  add, EditIni
Menu, CFG,  add, ReloadIni
Menu, TRAY, add, Config, :CFG ; Add submenu config
Menu, TRAY, add, Debug 	; Debug mode toggle
Menu, TRAY, add, Help 	; Display help info
Menu, TRAY, add, Exit 	; Exit app


;;;;;;;;;;;;;;;;;;;;;;;; Check If Ini File Exists
If (NOT FileExist(IniPath))
{
	MsgBox, 4096,, Ini file not found, using default one
	IniWrite, Emacs, %IniPath%, AppsKey, WinTitle
	IniWrite, Emacs, %IniPath%, AppsKey, WinClass
	IniWrite, C:\cygwin\bin\emacs-w32.exe, %IniPath%, AppsKey, ExePath
	IniWrite, bash, %IniPath%, RAlt, WinTitle
	IniWrite, mintty, %IniPath%, RAlt, WinClass
	IniWrite, C:\cygwin\bin\mintty -, %IniPath%, RAlt, ExePath
	IniWrite, Xshell, %IniPath%, RCtrl, WinTitle
	IniWrite, Xshell4:MainWnd, %IniPath%, RCtrl, WinClass
	IniWrite, C:\Program Files\NetSarang\Xshell 4\Xshell.exe, %IniPath%, RCtrl, ExePath
}

BindKeys(IniPath)
while true
{
	Sleep,250
}

BindKeys(IniPath)
{
	;;;;;;;;;;;;;;;;;;;;;;;; Read Hot Keys And Bind Them All
	IniRead, Keys, %IniPath%
	StringReplace, Keys, Keys, `n, %A_Space%, All  ; ` escapses "\n"
	; To split string with a substring delimeter, you've to replace substring with one character
	StringSplit, KeysArray, Keys, %A_Space%
	If DEBUGFLAG
		MsgBox, 4096,, %KeysArray0% %KeysArray1% %KeysArray2%

	i := 1
	While (i != KeysArray0+1)
	{	
		KeyName := KeysArray%i%
		HotKey, %KeyName%, PUF
		If	DEBUGFLAG
			MsgBox, 4096,, HotKey sucessfully bind
		i := i+1
	}
}

PUF:
	;; Pop Up Function
    ; Hotkey must bind with a label, function is not allowed
	; PUF has to be put after a while loop
	AppKey = %A_ThisHotkey%
	If DEBUGFLAG
		MsgBox,4096,, "Hotkey detected " . %AppKey%
	IniRead, AppTitle, %IniPath%, %AppKey%, WinTitle, Null
	IniRead, AppClass, %IniPath%,  %AppKey%, WinClass, Null
	IniRead, AppPath, %IniPath%,  %AppKey%, ExePath, Null
	If DEBUGFLAG
		MsgBox, %AppTitle% %AppClass% %AppPath%
	; hWND := WinExist("ahk_class" . AppClass)
	if WinExist("ahk_class" . AppClass) {
		hWND := WinExist("ahk_class" . AppClass)
	}
	else {
		hWND := WinExist(AppTitle)
	}
		
	If hWND
	{
		; WinSet, AlwaysOnTop, On, ahk_id %hWND%	; Set the window always on top
		WinGet, WinMinMax, MinMax, ahk_id %hWND%
		; Get the MinMax status of the window and store it in WinMinMax
		; MinMax=-1, minimized, use WinRestore to restore default status
		; MinMax=1, maximized, use WinRestore to restore default status
		; MinMax=0, neither minimized nor maximized
		If (WinMinMax = -1)	; If minimized, then restore and activate the window
		{
			if DEBUGFLAG
				MsgBox, 4096,, The window is minimized, restore and activate it!
			WinRestore, ahk_id %hWND%
			WinActivate, ahk_id %hWND%
		}
		Else
		{
            If WinActive("ahk_id" . hWND) ; If active, minimize it!
            {
			    if DEBUGFLAG
				    MsgBox, 4096,, The window is not minimized and active, minimize it!
			    WinMinimize, ahk_id %hWND%
            }
            Else ; If not minimized and not active, then activate it!
            {
                WinActivate, ahk_id %hWND%
            }

		}
	}
	else  ; If desired process not running, create one
	{
		if DEBUGFLAG
			MsgBox, 4096,, No matching window found, create one!
		Run,%AppPath%
		WinWait, %AppTitle%		; Wait untill window popup
		WinSet, AlwaysOnTop, Off 	; Set the new window always on top off
	}
return



;;;;;;;;;;;;;;;;;;;;;;; Tray Events Settings
OpenDir:
Run, explorer %A_ScriptDir%
Return

EditIni:
Run, open %IniPath%
Return

ReloadIni:
BindKeys(IniPath)
Return

Debug:
DEBUGFLAG := not DEBUGFLAG
If DEBUGFLAG
	MsgBox, 4096,, Debug mode is on.
Else
	MsgBox, 4096,, Debug mode is off.
Return

Help:
; Open url with default browser
; http://www.autohotkey.com/board/topic/30007-start-default-browser-with-blank-screen/
Run, https://github.com/voidmous/PUF/tree/ahk-devel?url=about:blank
Return

Exit:
ExitApp

Null:
return